plugins {
    id 'java'
}

dependencies {
    // Core stays vendor-free; shared interfaces and bridges live here.
    implementation "edu.wpi.first.wpilibj:wpilibj-java:${rootProject.ext.wpilibVersion}"
    implementation "edu.wpi.first.wpimath:wpimath-java:${rootProject.ext.wpilibVersion}"
    implementation "edu.wpi.first.apriltag:apriltag-java:${rootProject.ext.wpilibVersion}"
    implementation "edu.wpi.first.cameraserver:cameraserver-java:${rootProject.ext.wpilibVersion}"
    implementation "edu.wpi.first.cscore:cscore-java:${rootProject.ext.wpilibVersion}"
    implementation "edu.wpi.first.ntcore:ntcore-java:${rootProject.ext.wpilibVersion}"
    implementation "edu.wpi.first.wpiutil:wpiutil-java:${rootProject.ext.wpilibVersion}"
    implementation "edu.wpi.first.hal:hal-java:${rootProject.ext.wpilibVersion}"
    implementation "edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:${rootProject.ext.wpilibVersion}"
    implementation "edu.wpi.first.wpiunits:wpiunits-java:${rootProject.ext.wpilibVersion}"
    implementation "us.hebi.quickbuf:quickbuf-runtime:1.4"
    implementation "org.ejml:ejml-simple:${rootProject.ext.athenaVersions.ejml}"
    implementation "com.fasterxml.jackson.core:jackson-annotations:${rootProject.ext.athenaVersions.jackson}"
    implementation "com.fasterxml.jackson.core:jackson-core:${rootProject.ext.athenaVersions.jackson}"
    implementation "com.fasterxml.jackson.core:jackson-databind:${rootProject.ext.athenaVersions.jackson}"
    implementation "org.tomlj:tomlj:1.1.1"
    testImplementation "org.junit.jupiter:junit-jupiter:5.10.2"

}

def wpilibVersionText = String.valueOf(rootProject.ext.wpilibVersion)
def frcYearMatcher = (wpilibVersionText =~ /(20\d{2})/)
def frcYear = frcYearMatcher.find() ? frcYearMatcher.group(1) : "2026"
def wpilibMavenRoot = file("/home/${System.getenv('USER')}/wpilib/${frcYear}/maven")

def requireLatestNativeZip = { String glob ->
    def matches = fileTree(wpilibMavenRoot) {
        include glob
    }.files
    if (matches.isEmpty()) {
        throw new GradleException("Missing WPILib native zip for pattern '${glob}' under ${wpilibMavenRoot}")
    }
    return matches.sort().last()
}

def wpilibNativeZips = files(
        requireLatestNativeZip("edu/wpi/first/hal/hal-cpp/*/hal-cpp-*-linuxx86-64.zip"),
        requireLatestNativeZip("edu/wpi/first/wpiutil/wpiutil-cpp/*/wpiutil-cpp-*-linuxx86-64.zip"),
        requireLatestNativeZip("edu/wpi/first/wpinet/wpinet-cpp/*/wpinet-cpp-*-linuxx86-64.zip"),
        requireLatestNativeZip("edu/wpi/first/ntcore/ntcore-cpp/*/ntcore-cpp-*-linuxx86-64.zip"),
        requireLatestNativeZip("edu/wpi/first/cscore/cscore-cpp/*/cscore-cpp-*-linuxx86-64.zip"),
        requireLatestNativeZip("edu/wpi/first/wpimath/wpimath-cpp/*/wpimath-cpp-*-linuxx86-64.zip"),
        requireLatestNativeZip("edu/wpi/first/apriltag/apriltag-cpp/*/apriltag-cpp-*-linuxx86-64.zip"))

def extractedWpilibNativesDir = layout.buildDirectory.dir("wpilib-test-natives")

tasks.register("extractWpilibTestNatives", Copy) {
    from {
        wpilibNativeZips.collect { zipTree(it) }
    }
    include "linux/x86-64/shared/*.so"
    into extractedWpilibNativesDir
}

test {
    dependsOn tasks.named("extractWpilibTestNatives")
    useJUnitPlatform()
    def nativePath = extractedWpilibNativesDir.get().file("linux/x86-64/shared").asFile.absolutePath
    systemProperty "java.library.path", nativePath
    environment "LD_LIBRARY_PATH", nativePath
}
